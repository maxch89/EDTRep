#Область ОбработчикиСобытийФормы

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	ПолучитьЗначенияНастроек();
	ОбновитьСписокКонвертаций();
КонецПроцедуры
#КонецОбласти
#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура КонвертацииВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;  
	ПараметрыФормы = Новый Структура("парКонвертация", Элементы.Конвертации.ТекущиеДанные.Ссылка);
	ОткрытьФорму("Обработка.НастройкаПравилОбмена.Форма", ПараметрыФормы,, Элементы.Конвертации.ТекущиеДанные.Ссылка);
КонецПроцедуры

&НаКлиенте
Процедура ТекущаяКонфигурацияИсточникПриИзменении(Элемент)
	СохранитьЗначенияНастроек();
	ОбновитьСписокКонвертаций();
КонецПроцедуры

&НаКлиенте
Процедура ТекущаяКонфигурацияПриемникПриИзменении(Элемент)
	СохранитьЗначенияНастроек();
	ОбновитьСписокКонвертаций();
КонецПроцедуры

&НаКлиенте
Процедура ВРаботеПриИзменении(Элемент)
	СохранитьЗначенияНастроек();
	ОбновитьСписокКонвертаций();
КонецПроцедуры

#КонецОбласти
#Область СлужебныеПроцедурыИФункции
&НаСервере
Процедура СохранитьЗначенияНастроек()
	
	СтруктураНастроек = Новый Структура;
	СтруктураНастроек.Вставить("ТекущаяКонфигурацияИсточник", ТекущаяКонфигурацияИсточник);
	СтруктураНастроек.Вставить("ТекущаяКонфигурацияПриемник", ТекущаяКонфигурацияПриемник);
	СтруктураНастроек.Вставить("ВРаботе", ВРаботе);

	КонвертацияДанныхXDTOВызовСервера.ЗаписатьНастройкиПользователя(СтруктураНастроек);
	
КонецПроцедуры

&НаСервере
Процедура ПолучитьЗначенияНастроек()
	
	СтруктураНастроек = КонвертацияДанныхXDTOВызовСервера.НастройкиПользователя();
	ТекущаяКонфигурацияИсточник = СтруктураНастроек.ТекущаяКонфигурацияИсточник;
	ТекущаяКонфигурацияПриемник = СтруктураНастроек.ТекущаяКонфигурацияПриемник;
	ВРаботе = СтруктураНастроек.ВРаботе;
КонецПроцедуры

&НаСервере
Процедура ОбновитьСписокКонвертаций()
	Конвертации.Очистить();
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Конвертации.Ссылка,
	|	Конвертации.Наименование КАК Наименование
	|ИЗ
	|	Справочник.Конвертации КАК Конвертации
	|ГДЕ
	|УПОРЯДОЧИТЬ ПО
	|	Наименование";
	ТекстУсловия = "";
	Если ЗначениеЗаполнено(ТекущаяКонфигурацияИсточник) ИЛИ ЗначениеЗаполнено(ТекущаяКонфигурацияПриемник) Тогда
		ТекстУсловия = "Конфигурация В (&МассивКонфигураций)";
		МассивКонфигураций = Новый Массив;
		Если ЗначениеЗаполнено(ТекущаяКонфигурацияИсточник) Тогда
			МассивКонфигураций.Добавить(ТекущаяКонфигурацияИсточник);
		КонецЕсли;
		Если ЗначениеЗаполнено(ТекущаяКонфигурацияПриемник) Тогда
			МассивКонфигураций.Добавить(ТекущаяКонфигурацияПриемник);
		КонецЕсли;
		Запрос.УстановитьПараметр("МассивКонфигураций", МассивКонфигураций);
	КонецЕсли;
	Если ВРаботе Тогда
		Если ТекстУсловия <> "" Тогда
			ТекстУсловия = ТекстУсловия + " И ";
		КонецЕсли;
		ТекстУсловия = ТекстУсловия + "ВРаботе";
	КонецЕсли;
	
	Если ТекстУсловия = "" Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "ГДЕ", "");
	Иначе
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "ГДЕ", "ГДЕ " + ТекстУсловия);
	КонецЕсли;
	

	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		НовСтрока = Конвертации.Добавить();
		ЗаполнитьЗначенияСвойств(НовСтрока, Выборка);
	КонецЦикла;
КонецПроцедуры

#КонецОбласти




