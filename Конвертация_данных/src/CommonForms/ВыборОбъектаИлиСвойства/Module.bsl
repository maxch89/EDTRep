#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ПараметрыФормы = ЭтаФорма.Параметры;
	РежимРаботы = ПараметрыФормы.РежимРаботы;
	ВерсияФормата = Неопределено;
	Если ПараметрыФормы.Свойство("ВерсияФормата") Тогда
		ПереданаВерсияФормата = Истина;
		ВерсияФормата = ПараметрыФормы.ВерсияФормата;
	КонецЕсли;
	ТекущаяСтрока = ПараметрыФормы.ТекущаяСтрока;
	ПараметрыФормы.Свойство("ДляПКО", ДляПКО);
	ПараметрыФормы.Свойство("ДляПКПД", ДляПКПД);
	Элементы.СтраницаСвойстваФормата.Видимость = Ложь;
	Элементы.СтраницаОбъектыФормата.Видимость = Ложь;
	Элементы.СтраницаОбъектыКонфигурации.Видимость = Ложь;
	Элементы.СтраницаСвойстваКонфигурации.Видимость = Ложь;
	Если РежимРаботы = "ОбъектФормата" Тогда
		МассивКонвертаций = Неопределено;
		ПараметрыФормы.Свойство("МассивКонвертаций", МассивКонвертаций);
		ЗаполнитьОбъектыФормата(МассивКонвертаций);
		Элементы.СтраницаОбъектыФормата.Видимость = Истина;
		ЭтаФорма.Заголовок = НСтр("ru='Объекты формата'");
	ИначеЕсли РежимРаботы = "СвойствоФормата" Тогда
		ОбъектФормата = "";
		ПараметрыФормы.Свойство("ОбъектФормата", ОбъектФормата);
		ТаблицаСвойствФормата = Неопределено;
		ПараметрыФормы.Свойство("ТаблицаСвойствФормата", ТаблицаСвойствФормата);
		ЗаполнитьСвойстваФормата(ТаблицаСвойствФормата);
		Элементы.СтраницаСвойстваФормата.Видимость = Истина;
		ЭтаФорма.Заголовок = НСтр("ru='Свойства формата'");
	ИначеЕсли РежимРаботы = "ОбъектКонфигурации" Тогда
		МассивКонвертаций = Неопределено;
		ПараметрыФормы.Свойство("МассивКонвертаций", МассивКонвертаций);
		ЗаполнитьОбъектыКонфигурации(МассивКонвертаций);
		Элементы.СтраницаОбъектыКонфигурации.Видимость = Истина;
		ЭтаФорма.Заголовок = НСтр("ru='Объекты конфигурации'");
	ИначеЕсли РежимРаботы = "СвойствоКонфигурации" Тогда
		ТаблицаСвойствКонфигурации = Неопределено;
		ПараметрыФормы.Свойство("ТаблицаСвойствКонфигурации", ТаблицаСвойствКонфигурации);
		ЗаполнитьСвойстваКонфигурации(ТаблицаСвойствКонфигурации);
		Элементы.СтраницаСвойстваКонфигурации.Видимость = Истина;
		ЭтаФорма.Заголовок = НСтр("ru='Свойства конфигурации'");
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОбъектыФорматаВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	ОбработатьВыборИзСписка("ОбъектыФормата", "Наименование");
КонецПроцедуры

&НаКлиенте
Процедура ОбъектыФорматаВыборЗначения(Элемент, Значение, СтандартнаяОбработка)
	ОбработатьВыборИзСписка("ОбъектыФормата", "Наименование");
КонецПроцедуры

&НаКлиенте
Процедура СвойстваФорматаВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	ОбработатьВыборИзСписка("СвойстваФормата");
КонецПроцедуры

&НаКлиенте
Процедура СвойстваФорматаВыборЗначения(Элемент, Значение, СтандартнаяОбработка)
	ОбработатьВыборИзСписка("СвойстваФормата");
КонецПроцедуры

&НаКлиенте
Процедура ОбъектыКонфигурацииВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	ОбработатьВыборИзСписка("ОбъектыКонфигурации", "Наименование");
КонецПроцедуры

&НаКлиенте
Процедура ОбъектыКонфигурацииВыборЗначения(Элемент, Значение, СтандартнаяОбработка)
	ОбработатьВыборИзСписка("ОбъектыКонфигурации", "Наименование");
КонецПроцедуры

&НаКлиенте
Процедура СвойстваКонфигурацииВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	ОбработатьВыборИзСписка("СвойстваКонфигурации");
КонецПроцедуры

&НаКлиенте
Процедура СвойстваКонфигурацииВыборЗначения(Элемент, Значение, СтандартнаяОбработка)
	ОбработатьВыборИзСписка("СвойстваКонфигурации");
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции
&НаСервере
Процедура ЗаполнитьОбъектыФормата(МассивКонвертаций)
	Если ПереданаВерсияФормата Тогда
		КонвертацияДанныхXDTOВызовСервера.ЗаполнитьСписокОбъектовФормата(ОбъектыФормата, Неопределено, Ложь, Ложь, ВерсияФормата);
	Иначе
		КонвертацияДанныхXDTOВызовСервера.ЗаполнитьСписокОбъектовФормата(ОбъектыФормата, МассивКонвертаций, ДляПКО, ДляПКПД);
	КонецЕсли;
	Если ЗначениеЗаполнено(ТекущаяСтрока) Тогда
		СтрокиТаб = ОбъектыФормата.НайтиСтроки(Новый Структура("Наименование", ТекущаяСтрока));
		Если СтрокиТаб.Количество() > 0 Тогда
			ИдСтроки = СтрокиТаб[0].ПолучитьИдентификатор();
			Элементы.ОбъектыФормата.ТекущаяСтрока = ИдСтроки;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСвойстваФормата(ТаблицаСвойствФормата)
	Если ТаблицаСвойствФормата <> Неопределено Тогда
		// Уже готова таблица свойств (вызов из формы редактирования ПКС).
		Для Каждого СтрТаб Из ТаблицаСвойствФормата Цикл
			СтрНов = СвойстваФормата.Добавить();
			ЗаполнитьЗначенияСвойств(СтрНов, СтрТаб);
		КонецЦикла;
	Иначе
		Если ПереданаВерсияФормата Тогда
			КонвертацияДанныхXDTOВызовСервера.ЗаполнитьСписокСвойствФормата(СвойстваФормата, Неопределено, ОбъектФормата, ВерсияФормата);
		Иначе
			КонвертацияДанныхXDTOВызовСервера.ЗаполнитьСписокСвойствФормата(СвойстваФормата, МассивКонвертаций, ОбъектФормата);
		КонецЕсли;
	КонецЕсли;
	Если ЗначениеЗаполнено(ТекущаяСтрока) Тогда
		СтрокиТаб = СвойстваФормата.НайтиСтроки(Новый Структура("Имя", ТекущаяСтрока));
		Если СтрокиТаб.Количество() > 0 Тогда
			ИдСтроки = СтрокиТаб[0].ПолучитьИдентификатор();
			Элементы.СвойстваФормата.ТекущаяСтрока = ИдСтроки;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСвойстваКонфигурации(ТаблицаСвойствКонфигурации)
	// Уже готова таблица свойств (вызов из формы редактирования ПКС).
	Для Каждого СтрТаб Из ТаблицаСвойствКонфигурации Цикл
		СтрНов = СвойстваКонфигурации.Добавить();
		ЗаполнитьЗначенияСвойств(СтрНов, СтрТаб);
	КонецЦикла;
	Если ЗначениеЗаполнено(ТекущаяСтрока) Тогда
		СтрокиТаб = СвойстваКонфигурации.НайтиСтроки(Новый Структура("Имя", ТекущаяСтрока));
		Если СтрокиТаб.Количество() > 0 Тогда
			ИдСтроки = СтрокиТаб[0].ПолучитьИдентификатор();
			Элементы.СвойстваКонфигурации.ТекущаяСтрока = ИдСтроки;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьОбъектыКонфигурации(МассивКонвертаций)
	КонвертацияДанныхXDTOВызовСервера.ЗаполнитьСписокОбъектовКонфигурации(ОбъектыКонфигурации, МассивКонвертаций, ДляПКО, ДляПКПД);
	Если ЗначениеЗаполнено(ТекущаяСтрока) Тогда
		СтрокиТаб = ОбъектыКонфигурации.НайтиСтроки(Новый Структура("Наименование", ТекущаяСтрока));
		Если СтрокиТаб.Количество() > 0 Тогда
			ИдСтроки = СтрокиТаб[0].ПолучитьИдентификатор();
			Элементы.ОбъектыКонфигурации.ТекущаяСтрока = ИдСтроки;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьВыборИзСписка(ИмяТабПоля, ИмяКолонкиТабПоля = "Имя")
	ТекущееЗначение = Элементы[ИмяТабПоля].ТекущиеДанные[ИмяКолонкиТабПоля];
	ЭтаФорма.Закрыть(ТекущееЗначение);
КонецПроцедуры

#КонецОбласти

