#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	Заголовок = Параметры.ЗаголовокФормыВыбора;
	МассивПереданныхЗначений = Параметры.МассивВыбранныхЗначений;
	
	Для Каждого Элемент Из Параметры.МассивДоступныхЗначений Цикл
		
		НовоеЗначение = СписокВыбираемыхЗначений.Добавить();
		НовоеЗначение.Представление = Элемент;
		НовоеЗначение.Ключ = Элемент;
		НовоеЗначение.Идентификатор = Элемент;
		НовоеЗначение.Пометка = МассивПереданныхЗначений.Найти(Элемент) <> Неопределено;
		
	КонецЦикла;
	
	СписокВыбираемыхЗначений.Сортировать("Пометка Убыв, Представление");
	
КонецПроцедуры

#КонецОбласти
#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗаписатьЗакрыть(Команда)
	
	МассивВыбранныхЗначений = Новый Массив;
	Для Каждого Элемент Из СписокВыбираемыхЗначений Цикл
		Если Элемент.Пометка Тогда
			МассивВыбранныхЗначений.Добавить(Элемент.Идентификатор);
		КонецЕсли;
	КонецЦикла;
	
	ПараметрыЗакрытияФормы = Новый Структура();
	ПараметрыЗакрытияФормы.Вставить("МассивВыбранныхЗначений", МассивВыбранныхЗначений);
	
	ОповеститьОВыборе(ПараметрыЗакрытияФормы);
	
КонецПроцедуры

&НаКлиенте
Процедура СнятьОтметку(Команда)
	
	ЗаполнитьОтметки(Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтметитьВсе(Команда)
	
	ЗаполнитьОтметки(Истина);
	
КонецПроцедуры

#КонецОбласти
#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьОтметки(ЗначениеОтметки)
	
	ТаблицаЗаполняемыхЗначений = СписокВыбираемыхЗначений.Выгрузить();
	ТаблицаЗаполняемыхЗначений.ЗаполнитьЗначения(ЗначениеОтметки, "Пометка");
	СписокВыбираемыхЗначений.Загрузить(ТаблицаЗаполняемыхЗначений);
	
КонецПроцедуры
#КонецОбласти