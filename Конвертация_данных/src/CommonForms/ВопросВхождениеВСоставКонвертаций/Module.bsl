#Область ОбработчикиСобытийФормы
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	Если НЕ ЗначениеЗаполнено(ЭтаФорма.Параметры.ЭлементКонвертации)
		ИЛИ ЭтаФорма.Параметры.СписокКонвертаций.Количество() < 2 Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	ЭлементКонвертации = ЭтаФорма.Параметры.ЭлементКонвертации;
	Для Каждого ЭлементСписка Из ЭтаФорма.Параметры.СписокКонвертаций Цикл
		Конвертации.Добавить(ЭлементСписка.Значение,,Истина);
	КонецЦикла;
КонецПроцедуры
#КонецОбласти
#Область ОбработчикиКомандФормы
&НаКлиенте
Процедура ОК(Команда)
	ВыбранныеКонвертации = Новый СписокЗначений;
	Для Каждого ЭлементСписка Из Конвертации Цикл
		Если ЭлементСписка.Пометка Тогда
			ВыбранныеКонвертации.Добавить(ЭлементСписка.Значение);
		КонецЕсли;
	КонецЦикла;
	Если ВыбранныеКонвертации.Количество() < Конвертации.Количество() Тогда
		ОКНаСервере();
		ЭтаФорма.ОповеститьОВыборе(ВыбранныеКонвертации);
	Иначе
		ЭтаФорма.Закрыть();
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ОКНаСервере()
	// Создаем копию элемента до изменения
	ЭлементКонвертацииОбъект = ЭлементКонвертации.Скопировать();
	ЭлементКонвертацииОбъект.Записать();
	
	// Корректируем состав конвертации
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	Владелец КАК Конвертация,
	|	Ссылка
	|ИЗ Справочник.СоставыКонвертаций
	|ГДЕ ЭлементКонвертации = &ЭлементКонвертации И НЕ ПометкаУдаления И НЕ Отключить";
	Запрос.УстановитьПараметр("ЭлементКонвертации", ЭлементКонвертации);
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		РезультатПоиска = Конвертации.НайтиПоЗначению(Выборка.Конвертация);
		Если РезультатПоиска = Неопределено Тогда
			// Страховочная проверка. Такого не должно быть
			Продолжить;
		Иначе
			Если РезультатПоиска.Пометка Тогда
				// Оставляем в текущем состоянии
				Продолжить;
			Иначе
				СоставКонвертацийОбъект = Выборка.Ссылка.ПолучитьОбъект();
				СоставКонвертацийОбъект.ЭлементКонвертации = ЭлементКонвертацииОбъект.Ссылка;
				СоставКонвертацийОбъект.Записать();
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры
#КонецОбласти
