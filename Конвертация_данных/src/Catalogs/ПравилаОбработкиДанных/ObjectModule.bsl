#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
#Область ОбработчикиСобытий
Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	Если ТипЗнч(ДанныеЗаполнения) = Тип("СправочникСсылка.ПравилаКонвертацииОбъектов") Тогда
		ЭтотОбъект.Группа = ДанныеЗаполнения.Группа;
		Если ДанныеЗаполнения.ИспользоватьДляОтправки Тогда
			ЭтотОбъект.ИспользоватьДляОтправки = Истина;
			ЭтотОбъект.ОбъектВыборки = ДанныеЗаполнения.ОбъектКонфигурации;
		Иначе
			ЭтотОбъект.ИспользоватьДляПолучения = Истина;
			ЭтотОбъект.ОбъектВыборки = ДанныеЗаполнения.ОбъектФормата;
		КонецЕсли;
		ЭтотОбъект.Наименование = СокрЛП(ЭтотОбъект.ОбъектВыборки);
		ЭтотОбъект.ПравилоКонвертацииОбъекта = ДанныеЗаполнения.Код;
		
		ЗаполнитьСписокКонвертацийНовогоОбъекта(ЭтотОбъект, ДанныеЗаполнения);
	КонецЕсли;
КонецПроцедуры

Процедура ПередЗаписью(Отказ)
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;

	Если ЗначениеЗаполнено(АлгоритмВыборкаДанных) И НЕ ИспользоватьДляОтправки Тогда
		АлгоритмВыборкаДанных = "";
	КонецЕсли;
	Если ЗначениеЗаполнено(Ссылка) 
		И ПометкаУдаления <> Ссылка.ПометкаУдаления Тогда
		КонвертацияДанныхXDTOВызовСервера.ОбработатьПометкуНаУдалениеЭлементаКонвертации(Ссылка, ПометкаУдаления);
	КонецЕсли;
КонецПроцедуры
#КонецОбласти

#Область СлужебныеПроцедурыИФункции
Процедура ЗаполнитьСписокКонвертацийНовогоОбъекта(НовыйОбъект, ДанныеЗаполнения)
	СписокКонвертаций = Новый СписокЗначений();
	КонвертацияДанныхXDTOВызовСервера.ПолучитьСписокКонвертацийДляЭлементаКонвертации(ДанныеЗаполнения, СписокКонвертаций);
	
	СсылкаНового = НовыйОбъект.ПолучитьСсылкуНового();
	КонвертацияДанныхXDTOВызовСервера.ЗаписатьЭлементКонвертацииВСоставКонвертации(СсылкаНового, СписокКонвертаций);
КонецПроцедуры
#КонецОбласти
#КонецЕсли
