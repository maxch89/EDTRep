#Область ОбработчикиКомандФормы
&НаКлиенте
Процедура ПоднятьЭлементВверх(Команда)
	Если НЕ ЗначениеЗаполнено(Элементы.Список.ТекущаяСтрока) Тогда
		Возврат;
	КонецЕсли;
	
	СдвинутьЭлементНаСервере(Истина);
	Элементы.Список.Обновить();
КонецПроцедуры

&НаКлиенте
Процедура ОпуститьЭлементВниз(Команда)
	Если НЕ ЗначениеЗаполнено(Элементы.Список.ТекущаяСтрока) Тогда
		Возврат;
	КонецЕсли;

	СдвинутьЭлементНаСервере(Ложь);
	Элементы.Список.Обновить();
КонецПроцедуры
#КонецОбласти
#Область СлужебныеПроцедурыИФункции
&НаСервере
Процедура СдвинутьЭлементНаСервере(Вверх = Истина)
	ТекущаяСсылка = Элементы.Список.ТекущаяСтрока;
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ ПЕРВЫЕ 1
	|БлижайшиеЭлементы.Ссылка КАК БлижайшийЭлемент,
	|БлижайшиеЭлементы.Порядок КАК БлижайшийПорядок
	|ИЗ Справочник.ЭлементыОписания КАК БлижайшиеЭлементы
	|ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ЭлементыОписания КАК ТекущийЭлемент
	|ПО БлижайшиеЭлементы.Родитель = ТекущийЭлемент.Родитель
	|	И БлижайшиеЭлементы.Владелец = ТекущийЭлемент.Владелец
	|	И БлижайшиеЭлементы.Порядок "+?(Вверх,"<", ">") +" ТекущийЭлемент.Порядок 
	|ГДЕ ТекущийЭлемент.Ссылка = &Ссылка И БлижайшиеЭлементы.ПометкаУдаления = ЛОЖЬ
	|УПОРЯДОЧИТЬ ПО БлижайшиеЭлементы.Порядок " + ?(Вверх,"УБЫВ", "ВОЗР");
	Запрос.УстановитьПараметр("Ссылка", ТекущаяСсылка);
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат;
	КонецЕсли;
	Выборка = РезультатЗапроса.Выбрать();
	Выборка.Следующий();
	// Меняем местами порядок у текущего и найденного элемента
	ОбъектТекущий = ТекущаяСсылка.ПолучитьОбъект();
	ОбъектБлижайший = Выборка.БлижайшийЭлемент.ПолучитьОбъект();
	НовыйПорядок = Выборка.БлижайшийПорядок;
	СтарыйПорядок = ТекущаяСсылка.Порядок;
	ОбъектТекущий.Порядок = НовыйПорядок;
	ОбъектБлижайший.Порядок = СтарыйПорядок;
	ОбъектТекущий.Записать();
	ОбъектБлижайший.Записать();
КонецПроцедуры
#КонецОбласти