#Область ОбработчикиСобытийФормы
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	// СтандартныеПодсистемы.ВерсионированиеОбъектов
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов
	Если ЗначениеЗаполнено(Объект.Ссылка)
		И Объект.ТипЭлемента <> Перечисления.ТипыЭлементовОписания.ОписаниеОбъектаФормата Тогда
		ТекстОписания = Объект.Ссылка.ТекстПростогоОписания.Получить();
	КонецЕсли;
	Если НЕ ЗначениеЗаполнено(Объект.Ссылка) Тогда
		СкопироватьОформлениеЗаголовка();
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ
	|Наименование
	|ИЗ Справочник.ОбъектыФормата
	|ГДЕ НЕ ПометкаУдаления И Владелец.Наименование = &НаименованиеВерсии";
	Запрос.УстановитьПараметр("НаименованиеВерсии", СокрЛП(Объект.Владелец.НомерВерсииФормата));
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Элементы.ОбъектФормата.СписокВыбора.Добавить(Выборка.Наименование);
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	Если ТекущийОбъект.ТипЭлемента <> Перечисления.ТипыЭлементовОписания.ОписаниеОбъектаФормата Тогда
		ХранилищеФорматированногоДокумента = Новый ХранилищеЗначения(ТекстОписания);
		ТекущийОбъект.ТекстПростогоОписания = ХранилищеФорматированногоДокумента;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	ОбновитьВидимостьДоступность();
КонецПроцедуры
#КонецОбласти
#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ТипЭлементаПриИзменении(Элемент)
	ОбновитьВидимостьДоступность();
	СкопироватьОформлениеЗаголовка();
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьВидимостьДоступность()
	ОписаниеОбъектаФормата = (Объект.ТипЭлемента = ПредопределенноеЗначение("Перечисление.ТипыЭлементовОписания.ОписаниеОбъектаФормата"));
	Элементы.ГруппаСтраницаОписание.Доступность = НЕ ОписаниеОбъектаФормата;
	Элементы.ГруппаОбъектФормата.Видимость = ОписаниеОбъектаФормата;
	Элементы.УровеньЗаголовка.Видимость = (Объект.ТипЭлемента = ПредопределенноеЗначение("Перечисление.ТипыЭлементовОписания.Заголовок"));
КонецПроцедуры

&НаСервере
Процедура СкопироватьОформлениеЗаголовка()
	Если Объект.ТипЭлемента <> Перечисления.ТипыЭлементовОписания.Заголовок Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого СтрокаОбразец Из Объект.Владелец.ОформлениеЗаголовков Цикл
		Если СтрокаОбразец.Уровень = Объект.УровеньЗаголовка Тогда
			ТекстОписания = СтрокаОбразец.ОбразецОформления.Получить();
			Прервать;
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры


&НаКлиенте
Процедура ОбъектФорматаОткрытие(Элемент, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	Если НЕ ЗначениеЗаполнено(Объект.ОбъектФормата) Тогда
		Возврат;
	КонецЕсли;
	
	СсылкаНаОбъектФормата = ПолучитьСсылкуНаОбъектФормата(Объект.ОбъектФормата, Объект.Владелец);
	Если НЕ ЗначениеЗаполнено(СсылкаНаОбъектФормата) Тогда
		Возврат;
	КонецЕсли;
	
	ОткрытьФорму("Справочник.ОбъектыФормата.Форма.ФормаЭлемента", Новый Структура("Ключ", СсылкаНаОбъектФормата));
КонецПроцедуры
#КонецОбласти
#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Функция ПолучитьСсылкуНаОбъектФормата(ИмяОбъектФормата, ОписаниеСсылка)
		Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ ПЕРВЫЕ 1
	|Ссылка
	|ИЗ Справочник.ОбъектыФормата
	|ГДЕ НЕ ПометкаУдаления И Владелец.Наименование = &НаименованиеВерсии
	|	И Наименование = &НаименованиеОбъекта";
	Запрос.УстановитьПараметр("НаименованиеВерсии", СокрЛП(ОписаниеСсылка.НомерВерсииФормата));
	Запрос.УстановитьПараметр("НаименованиеОбъекта", СокрЛП(ИмяОбъектФормата));
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат Выборка.Ссылка;
	КонецЕсли;
	Возврат Неопределено;
КонецФункции
#КонецОбласти
