#Область ОбработчикиСобытийФормы
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	// СтандартныеПодсистемы.ВерсионированиеОбъектов
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

	Если ЗначениеЗаполнено(Объект.Ссылка) Тогда
		ТекстОписания = Объект.Ссылка.Описание.Получить();
	КонецЕсли;
	
	Если ЭтаФорма.Параметры.Свойство("ПараметрыДерева") Тогда
		ПараметрыДерева = ЭтаФорма.Параметры.ПараметрыДерева;
		Если ПараметрыДерева.Свойство("ОбъектКопирования") Тогда
			ОбъектКопирования = ЭтаФорма.Параметры.ПараметрыДерева.ОбъектКопирования;
			ЗаполнитьЗначенияСвойств(Объект, ОбъектКопирования,"Владелец, Наименование, ПространствоИмен, ВключатьВОписание, ПространствоИменБазовогоТипа");
			Для Каждого СтрТаб Из ОбъектКопирования.Состав Цикл
				НовСтр = Объект.Состав.Добавить();
				НовСтр.ТипФормата = СтрТаб.ТипФормата;
			КонецЦикла;
			ТекстОписания = ОбъектКопирования.Описание.Получить();
		Иначе
			Если ПараметрыДерева.Свойство("ПространствоИменБазовогоТипа") Тогда
				Объект.ПространствоИменБазовогоТипа = ПараметрыДерева.ПространствоИменБазовогоТипа;
			КонецЕсли;
			Если ПараметрыДерева.Свойство("БазовыйТип") Тогда
				Объект.БазовыйТип = ПараметрыДерева.БазовыйТип;
			КонецЕсли;
			Если ПараметрыДерева.Свойство("ВерсияФормата") Тогда
				Объект.Владелец = ПараметрыДерева.ВерсияФормата;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	Если НЕ ЗначениеЗаполнено(Объект.ПространствоИмен) И ЗначениеЗаполнено(Объект.Владелец) Тогда
		Если ЗначениеЗаполнено(Объект.Владелец.ПространствоИмен) Тогда
			Объект.ПространствоИмен = Объект.Владелец.ПространствоИмен;
		Иначе
			Объект.ПространствоИмен = "http://v8.1c.ru/edi/edi_stnd/EnterpriseData/" + СокрЛП(Объект.Владелец.Наименование);
		КонецЕсли;
	КонецЕсли;

	ПространствоИменПростойТип = "http://www.w3.org/2001/XMLSchema";
	ПространствоИменСсылочныйТип = "http://www.1c.ru/SSL/Exchange/Message";
	Если НЕ ЗначениеЗаполнено(Объект.ПространствоИменБазовогоТипа) Тогда
		Объект.ПространствоИменБазовогоТипа = ПространствоИменПростойТип;
	КонецЕсли;
	Если Объект.СоставнойТип Тогда
		БазовыйТипВариант = 3;
	Иначе
		БазовыйТипВариант = ?(Объект.ПространствоИменБазовогоТипа = ПространствоИменСсылочныйТип,1,2);
	КонецЕсли;
	Если БазовыйТипВариант = 2 И ЗначениеЗаполнено(Объект.БазовыйТип) Тогда
		КонвертацияДанныхXDTOСервер.РазложитьПредставлениеПростогоТипа(НРег(Объект.БазовыйТип),ТипБазовогоТипа, ДлинаБазовогоТипа, ТочностьБазовогоТипа, МинимальнаяДлинаСтроки, НеограниченнаяДлина);
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	ХранилищеФорматированногоДокумента = Новый ХранилищеЗначения(ТекстОписания);
	ТекущийОбъект.Описание = ХранилищеФорматированногоДокумента;
	Если НЕ Объект.СоставнойТип Тогда
		Объект.Состав.Очистить();
		СтрТаб = Объект.Состав.Добавить();
		СтрТаб.типФормата = Объект.БазовыйТип;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	УстановитьДоступностьЭлементовФормы();
КонецПроцедуры
#КонецОбласти
#Область ОбработчикиСобытийЭлементовШапкиФормы
&НаКлиенте
Процедура БазовыйТипВариантПриИзменении(Элемент)
	Если БазовыйТипВариант = 1 Тогда
		Объект.ПространствоИменБазовогоТипа = ПространствоИменСсылочныйТип;
		Объект.БазовыйТип = "Ref";
	Иначе
		Объект.ПространствоИменБазовогоТипа = ПространствоИменПростойТип;
		Объект.БазовыйТип = "";
	КонецЕсли;
	ЭтаФорма.Модифицированность = Истина;
	УстановитьДоступностьЭлементовФормы();
КонецПроцедуры

&НаКлиенте
Процедура ТипБазовогоТипаПриИзменении(Элемент)
	Если НЕ (ТипБазовогоТипа = "Строка" ИЛИ ТипБазовогоТипа = "Число") Тогда
		ДлинаБазовогоТипа = 0;
	КонецЕсли;
	Если ТипБазовогоТипа <> "Строка" Тогда
		МинимальнаяДлинаСтроки = 0;
		НеограниченнаяДлина = Ложь;
	КонецЕсли;
	Если ТипБазовогоТипа <> "Число" Тогда
		ТочностьБазовогоТипа = 0;
	КонецЕсли;
	ОбновитьНаименованиеБазовогоТипа();
	УстановитьДоступностьЭлементовФормы();
КонецПроцедуры

&НаКлиенте
Процедура ДлинаБазовогоТипаПриИзменении(Элемент)
	ОбновитьНаименованиеБазовогоТипа();
КонецПроцедуры

&НаКлиенте
Процедура ТочностьБазовогоТипаПриИзменении(Элемент)
	ОбновитьНаименованиеБазовогоТипа();
КонецПроцедуры

&НаКлиенте
Процедура НеограниченнаяДлинаПриИзменении(Элемент)
	Если НеограниченнаяДлина Тогда
		ДлинаБазовогоТипа = 0;
		МинимальнаяДлинаСтроки = 0;
	КонецЕсли;
	ОбновитьНаименованиеБазовогоТипа();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции
&НаКлиенте
Процедура УстановитьДоступностьЭлементовФормы()
	Элементы.ГруппаПростойТип.Видимость = (БазовыйТипВариант = 2);
	Элементы.ГруппаСоставнойТип.Видимость = (БазовыйТипВариант = 3);
	Элементы.ПространствоИменБазовогоТипа.Видимость = (БазовыйТипВариант <> 3);
	Если БазовыйТипВариант = 2 Тогда
		Элементы.БазовыйТип.Доступность = Ложь;
		Элементы.ТипыПростые.Видимость = Ложь;
		Если ТипБазовогоТипа = "Строка" Тогда
			Элементы.ТипыПростые.Видимость = Истина;
			Элементы.типыпростые.ТекущаяСтраница = Элементы.ТипСтрока;
		ИначеЕсли ТипБазовогоТипа = "Число" Тогда
			Элементы.ТипыПростые.Видимость = Истина;
			Элементы.типыпростые.ТекущаяСтраница = Элементы.ТипЧисло;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьНаименованиеБазовогоТипа()
	Объект.БазовыйТип = КонвертацияДанныхXDTOКлиент.СоставляющиеПростогоТипаВСтроку(ТипБазовогоТипа, ДлинаБазовогоТипа, ТочностьБазовогоТипа, МинимальнаяДлинаСтроки);
	ЭтаФорма.Модифицированность = Истина;
КонецПроцедуры

#КонецОбласти