
#Область ПрограммныйИнтерфейс
// Формирует текст запроса и параметры запроса по узлам обмена.
// Для контроля прохождения фильтра ПРО.
// Параметры:
//   Объект - Произвольный - СправочникОбъект, который выгружается.
//   ТекстЗапроса - Строка - Сформированный текст запроса.
//   ПараметрыЗапроса - Структура - Полученные параметры запроса.
Процедура ЗаполнитьПараметрыЗапроса(Объект, ТекстЗапроса, ПараметрыЗапроса) Экспорт
	
	ПараметрыЗапроса = ОпределитьПараметрыЗапросаОбъекта(Объект);
	ТекстЗапроса     = СформироватьТекстЗапросаПравилРегистрации();
	
КонецПроцедуры

// Определяет параметры запроса ПРО для выгружаемых объектов.
// Параметры:
//   ЭлементДанных - Произвольный - СправочникОбъект, который выгружается.
//   ТипЗначенияОбъекта - Тип - Тип выгружаемого объекта.
// Возвращаемое значение:
//  ПараметрыЗапроса - Структура - Параметры запроса.
//    Отказ                 - Отказ от выполнения регистрации.
//    Конвертации           - Массив конвертаций объекта.
//    ФильтрПоКонвертации   - Булево Определяет применение к объекту фильтра по конвертации.
//
Функция ОпределитьПараметрыЗапросаОбъекта(ЭлементДанных, ТипЗначенияОбъекта = Неопределено) Экспорт
	
	ПараметрыЗапроса = Новый Структура;
	ПараметрыЗапроса.Вставить("Отказ", Ложь);
	
	ПараметрыЗапроса.Вставить("Конвертации", Новый Массив);
	
	ПараметрыЗапроса.Вставить("ФильтрПоКонвертации", Истина);
	
	Если Не ЗначениеЗаполнено(ТипЗначенияОбъекта) Тогда
		ТипЗначенияОбъекта = ТипЗнч(ЭлементДанных);
	КонецЕсли;
	// Данные, которые привязаны к конвертации.
	Если ТипЗначенияОбъекта = Тип("СправочникОбъект.Алгоритмы")
		Или ТипЗначенияОбъекта = Тип("СправочникОбъект.ПравилаКонвертацииОбъектов")
		Или ТипЗначенияОбъекта = Тип("СправочникОбъект.ПравилаКонвертацииПредопределенныхДанных")
		Или ТипЗначенияОбъекта = Тип("СправочникОбъект.ПравилаОбработкиДанных") Тогда
		// Привязка через справочник "СоставыКонвертаций".
		ЗначенияФильтра_ПравилаОбмена(ЭлементДанных, ПараметрыЗапроса);
	ИначеЕсли ТипЗначенияОбъекта = Тип("СправочникОбъект.Конвертации") Тогда
		// Непосредственная связь.
		МассивКонвертаций = Новый Массив;
		МассивКонвертаций.Добавить(ЭлементДанных.Ссылка);
		ПараметрыЗапроса.Конвертации = МассивКонвертаций
	ИначеЕсли ТипЗначенияОбъекта = Тип("СправочникОбъект.СоставыКонвертаций") Тогда
		// Связь через владельца.
		МассивКонвертаций = Новый Массив;
		МассивКонвертаций.Добавить(ЭлементДанных.Владелец);
		ПараметрыЗапроса.Конвертации = МассивКонвертаций
	ИначеЕсли ТипЗначенияОбъекта = Тип("СправочникОбъект.Конфигурации") Тогда
		// Связь через реквизит Конвертации.
		ЗначенияФильтра_Конфигурации(ЭлементДанных.Ссылка, ПараметрыЗапроса);
	ИначеЕсли ТипЗначенияОбъекта = Тип("СправочникОбъект.Объекты") Тогда
		ЗначенияФильтра_Конфигурации(ЭлементДанных.Владелец, ПараметрыЗапроса);
	ИначеЕсли ТипЗначенияОбъекта = Тип("СправочникОбъект.Значения")
		ИЛИ ТипЗначенияОбъекта = Тип("СправочникОбъект.Свойства") Тогда
		ЗначенияФильтра_Конфигурации(ЭлементДанных.Владелец.Владелец, ПараметрыЗапроса);
	Иначе
		// Нет связи с конвертацией.
		ПараметрыЗапроса.Вставить("ФильтрПоКонвертации", Ложь);
	КонецЕсли;
	
	Возврат ПараметрыЗапроса;
	
КонецФункции

// Возвращает текст запроса ПРО для использования его в обработчиках правил регистрации
//
// Возвращаемое значение:
//  Строка - Текст запроса ПРО.
//
Функция СформироватьТекстЗапросаПравилРегистрации() Экспорт
	
	ТекстЗапроса = "ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ПланОбменаОсновнаяТаблица.Ссылка КАК Ссылка
		|ИЗ
		|	ПланОбмена.Полный КАК ПланОбменаОсновнаяТаблица
		|		ЛЕВОЕ СОЕДИНЕНИЕ ПланОбмена.Полный.Конвертации КАК ПланОбменаТаблицаКонвертации
		|		ПО ПланОбменаОсновнаяТаблица.Ссылка = ПланОбменаТаблицаКонвертации.Ссылка
		|ГДЕ
		|	НЕ ПланОбменаОсновнаяТаблица.Ссылка = &ПолныйЭтотУзел
		|	И НЕ ПланОбменаОсновнаяТаблица.ПометкаУдаления
		|	И (НЕ &СвойствоОбъекта_ФильтрПоКонвертации
		|			ИЛИ (ПланОбменаТаблицаКонвертации.Конвертация В (&СвойствоОбъекта_Конвертации)
		|				ИЛИ НЕ ПланОбменаОсновнаяТаблица.ИспользоватьОтборПоКонвертациям))
		|";
	
	Возврат ТекстЗапроса;
	
КонецФункции
#КонецОбласти
#Область СлужебныеПроцедурыИФункции

Процедура ЗначенияФильтра_ПравилаОбмена(Объект, ПараметрыЗапроса)
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Владелец КАК Конвертация
	|ИЗ Справочник.СоставыКонвертаций
	|ГДЕ Отключить = ЛОЖЬ И ПометкаУдаления = ЛОЖЬ И ЭлементКонвертации = &Ссылка";
	Запрос.УстановитьПараметр("Ссылка", Объект.Ссылка);
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат;
	КонецЕсли;
	ПараметрыЗапроса.Конвертации = РезультатЗапроса.Выгрузить().ВыгрузитьКолонку("Конвертация");
КонецПроцедуры

Процедура ЗначенияФильтра_Конфигурации(КонфигурацияСсылка, ПараметрыЗапроса)
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Ссылка КАК Конвертация
	|ИЗ Справочник.Конвертации
	|ГДЕ ПометкаУдаления = ЛОЖЬ И Конфигурация = &Ссылка";
	Запрос.УстановитьПараметр("Ссылка", КонфигурацияСсылка);
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат;
	КонецЕсли;
	ПараметрыЗапроса.Конвертации = РезультатЗапроса.Выгрузить().ВыгрузитьКолонку("Конвертация");
КонецПроцедуры

#КонецОбласти
