#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	// Подставим версию по умолчанию
	ВыборкаВерсии = Справочники.ВерсииФормата.Выбрать();
	Если ВыборкаВерсии.Следующий() Тогда
		Объект.ВерсияФормата = ВыборкаВерсии.Ссылка;
		ОбновитьДеревоФормата();
	Иначе
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Не обнаружено ни одной версии формата. Использование данной обработки невозможно.'"),,,,Отказ);
	КонецЕсли;
	
КонецПроцедуры
#КонецОбласти
#Область ОбработчикиСобытийЭлементовШапкиФормы
&НаКлиенте
Процедура ВерсияФорматаПриИзменении(Элемент)
	ОбновитьДеревоФормата();
КонецПроцедуры
#КонецОбласти
#Область ОбработчикиСобытийЭлементовТаблицыФормы
&НаКлиенте
Процедура ДеревоФорматаПриАктивизацииСтроки(Элемент)
	СсылкаНаОбъект = Элементы.ДеревоФормата.ТекущиеДанные.СсылкаНаОбъект;
	СтруктураДанныхСсылки = ПрочитатьДанныеСсылкиНаСервере(СсылкаНаОбъект);
	Описание = СтруктураДанныхСсылки.Описание;
	ПорядокСвойства = СтруктураДанныхСсылки.ПорядокСвойства;
КонецПроцедуры
#КонецОбласти
#Область ОбработчикиКомандФормы
&НаКлиенте
Процедура СохранитьДанныеСтроки(Команда)
	СсылкаНаОбъект = Элементы.ДеревоФормата.ТекущиеДанные.СсылкаНаОбъект;
	СохранитьДанныеСтрокиНаСервере(СсылкаНаОбъект);
КонецПроцедуры
#КонецОбласти
#Область СлужебныеПроцедурыИФункции
&НаСервере
Процедура ОбновитьДеревоФормата()
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Объекты.Ссылка КАК Объект,
	|	Свойства.Родитель КАК Родитель,
	|	Свойства.Ссылка КАК Свойство
	|ИЗ Справочник.ОбъектыФормата КАК Объекты
	|ЛЕВОЕ СОЕДИНЕНИЕ Справочник.СвойстваФормата КАК Свойства
	|ПО Объекты.Ссылка = Свойства.Владелец
	|ГДЕ НЕ Объекты.ПометкаУдаления И НЕ Свойства.ПометкаУдаления
	|	И Объекты.Владелец = &ВерсияФормата
	|ИТОГИ ПО Объекты.Ссылка, Свойства.Ссылка ИЕРАРХИЯ";
	Запрос.УстановитьПараметр("ВерсияФормата", Объект.ВерсияФормата);
	ВыборкаОбъекты = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Уровень0 = ДеревоФормата.ПолучитьЭлементы();
	Уровень0.Очистить();
	
	Пока ВыборкаОбъекты.Следующий() Цикл
		ЭлементУровня0 = Уровень0.Добавить();
		ЭлементУровня0.СсылкаНаОбъект = ВыборкаОбъекты.Объект;
		ВыборкаСвойства = ВыборкаОбъекты.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкамСИерархией);
		ЭлементГруппа = Неопределено;
		Пока ВыборкаСвойства.Следующий() Цикл
			Если ВыборкаСвойства.Свойство.ЭтоГруппа ИЛИ НЕ ЗначениеЗаполнено(ВыборкаСвойства.Свойство.Родитель) Тогда
				ТекУровень = ЭлементУровня0.ПолучитьЭлементы();
				ЭлементТекУровня = ТекУровень.Добавить();
				ЭлементТекУровня.СсылкаНаОбъект = ВыборкаСвойства.Свойство;
				Если ВыборкаСвойства.Свойство.ЭтоГруппа Тогда
					ЭлементГруппа = ЭлементТекУровня;
				КонецЕсли;
			Иначе
				ТекУровень = ЭлементГруппа.ПолучитьЭлементы();
				ЭлементТекУровня = ТекУровень.Добавить();
				ЭлементТекУровня.Родитель = ЭлементГруппа;
				ЭлементТекУровня.СсылкаНаОбъект = ВыборкаСвойства.Свойство;
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПрочитатьДанныеСсылкиНаСервере(СсылкаНаОбъект)
	СтруктураВозврат = Новый Структура("Описание, ПорядокСвойства",,0);
	СтруктураВозврат.Описание = СсылкаНаОбъект.Описание.Получить();
	Если ТипЗнч(СсылкаНаОбъект) = Тип("СправочникСсылка.СвойстваФормата") Тогда
		СтруктураВозврат.ПорядокСвойства = СсылкаНаОбъект.Порядок;
	КонецЕсли;
	Возврат СтруктураВозврат;
КонецФункции

&НаСервере
Процедура СохранитьДанныеСтрокиНаСервере(СсылкаНаОбъект)
	СпрОбъект = СсылкаНаОбъект.ПолучитьОбъект();
	СпрОбъект.Описание = Новый ХранилищеЗначения(Описание);
	Если ТипЗнч(СсылкаНаОбъект) = Тип("СправочникСсылка.СвойстваФормата") Тогда
		СпрОбъект.Порядок = ПорядокСвойства;
	КонецЕсли;
	СпрОбъект.Записать();
	// Вставить содержимое обработчика.
КонецПроцедуры

#КонецОбласти