#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если Параметры.Свойство("парКонфигурация") И ЗначениеЗаполнено(Параметры.парКонфигурация) Тогда
		Объект.Конфигурация = Параметры.ПарКонфигурация;
		СпособЗагрузки = 1;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	Если ЗначениеЗаполнено(Объект.Конфигурация) Тогда
		СпособЗагрузки = 1;
	КонецЕсли;
	
	УстановитьВидимость();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ИмяФайлаЗагрузкиНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	
	НастройкиДиалога = Новый Структура;
	НастройкиДиалога.Вставить("Фильтр", НСтр("ru = 'Файл данных (*.xml)'") + "|*.xml" );
	НастройкиДиалога.Вставить("Заголовок", НСтр("ru='Выберите файл со структурой конфигурации'"));

	
	Оповещение = Новый ОписаниеОповещения("ЗавершениеВыбораФайлаЗагрузки", ЭтотОбъект);
	ОбменДаннымиКлиент.ВыбратьИПередатьФайлНаСервер(Оповещение, НастройкиДиалога, УникальныйИдентификатор);
КонецПроцедуры

&НаКлиенте
Процедура СпособЗагрузкиПриИзменении(Элемент)
	УстановитьВидимость();
КонецПроцедуры
#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ВыполнитьЗагрузку(Команда)
	Если НЕ ЗначениеЗаполнено(Объект.ИмяФайлаЗагрузки) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru='Не выбран файл со структурой конфигурации'"));
		Возврат;
	КонецЕсли;
	Если СпособЗагрузки = 1 И НЕ ЗначениеЗаполнено(Объект.Конфигурация) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru='Не выбрана конфигурация для загрузки структуры'"));
		Возврат;
	КонецЕсли;
	Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.Ожидание;
	ИзменитьДоступностьЭлементовФормы(Ложь);
	ПодключитьОбработчикОжидания("ЗапускОбработки", 5, Истина);

КонецПроцедуры
#КонецОбласти

#Область СлужебныеПроцедурыИФункции
&НаКлиенте
Процедура ЗапускОбработки()
	ВыполнитьЗагрузкуСервер(СпособЗагрузки);
	ПодключитьОбработчикОжидания("ОбработчикОжиданияДлительнойОперации", 5, Истина);
КонецПроцедуры

&НаКлиенте
Процедура ОбработчикОжиданияДлительнойОперации()
	Если НЕ ДлительнаяОперация Тогда
		Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.Основная;
		ИзменитьДоступностьЭлементовФормы(Истина);
		Возврат;
	КонецЕсли;
	Если ДлительнаяОперацияВыполнена() Тогда
		ДлительнаяОперация = Ложь;
		ПодключитьОбработчикОжидания("ОкончаниеЗагрузкиКонфигурации", 1, Истина);
	Иначе
		ПодключитьОбработчикОжидания("ОбработчикОжиданияДлительнойОперации", 5, Истина);
	КонецЕсли;
КонецПроцедуры

&НаСервере
Функция ДлительнаяОперацияВыполнена()
	Возврат ДлительныеОперации.ЗаданиеВыполнено(ИдентификаторЗадания);
КонецФункции

&НаКлиенте
Процедура ОкончаниеЗагрузкиКонфигурации()
	РезультатЗагрузки = ПолучитьИзВременногоХранилища(АдресХранилищаРезультата);
	Если РезультатЗагрузки Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru='Загрузка структуры конфигурации выполнена успешно'"));
	Иначе
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru='Загрузка структуры конфигурации не выполнена'"));
	КонецЕсли;
	Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.Основная;
	ИзменитьДоступностьЭлементовФормы(Истина);
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьДоступностьЭлементовФормы(ФлагДоступность)
	Элементы.ФормаВыполнитьЗагрузку.Доступность = ФлагДоступность;
КонецПроцедуры

&НаКлиенте
Процедура ЗавершениеВыбораФайлаЗагрузки(Знач РезультатПомещенияФайлов, Знач ДополнительныеПараметры) Экспорт
	АдресФайлаЗагрузки = РезультатПомещенияФайлов.Хранение;
	ТекстОшибки           = РезультатПомещенияФайлов.ОписаниеОшибки;
	
	Объект.ИмяФайлаЗагрузки = РезультатПомещенияФайлов.Имя;
	Если ПустаяСтрока(ТекстОшибки) И ПустаяСтрока(АдресФайлаЗагрузки) Тогда
		ТекстОшибки = НСтр("ru = 'Ошибка передачи файла структуры конфигурации на сервер'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки, , "Объект.ИмяФайлаЗагрузки");
	КонецЕсли;
	
	ОбновитьОтображениеДанных();

КонецПроцедуры
&НаКлиенте
Процедура УстановитьВидимость()
	ЗагружатьВФормат = (СпособЗагрузки = 1);
	Элементы.ВерсияФормата.Видимость = ЗагружатьВФормат;
	Элементы.ДобавлятьТолькоНовыеОбъектыСвойстваЗначения.Видимость = ЗагружатьВФормат;
КонецПроцедуры

&НаСервере
Процедура ВыполнитьЗагрузкуСервер(СпособЗагрузки)
	ИдентификаторЗадания = Неопределено;
	ДлительнаяОперация = Ложь;
	АдресХранилищаРезультата = Неопределено;
	ДвоичныеДанные = ПолучитьИзВременногоХранилища(АдресФайлаЗагрузки);
	ИмяФайлаЗагрузки = ПолучитьИмяВременногоФайла("xml");
	ДвоичныеДанные.Записать(ИмяФайлаЗагрузки);
	СтруктураПараметров = Новый Структура("СпособЗагрузки, ИмяФайлаЗагрузки",СпособЗагрузки, ИмяФайлаЗагрузки);
	СтруктураПараметров.Вставить("Конфигурация", Объект.Конфигурация);
	СтруктураПараметров.Вставить("ДобавлятьТолькоНовые", Объект.ДобавлятьТолькоНовыеОбъектыСвойстваЗначения);
	Попытка
		Результат = ДлительныеОперации.ЗапуститьВыполнениеВФоне(
			УникальныйИдентификатор,
			"Обработки.ЗагрузкаСтруктурыКонфигурации.ВыполнитьЗагрузкуМетаданных",
			СтруктураПараметров,
			НСтр("ru = 'Загрузка структуры метаданных'"));
		АдресХранилищаРезультата = Результат.АдресХранилища;
		Если Результат.ЗаданиеВыполнено Тогда
			РезультатЗагрузки = ПолучитьИзВременногоХранилища(АдресХранилищаРезультата);
			Если РезультатЗагрузки Тогда
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru='Загрузка структуры конфигурации выполнена успешно'"));
			Иначе
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru='Загрузка структуры конфигурации не выполнена'"));
			КонецЕсли;
		Иначе
			ДлительнаяОперация = Истина;
			ИдентификаторЗадания = Результат.ИдентификаторЗадания;
		КонецЕсли;
	Исключение
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'При загрузке структуры конфигурации произошла ошибка'") + Символы.ПС + ОписаниеОшибки());
		Возврат;
	КонецПопытки
КонецПроцедуры
#КонецОбласти
